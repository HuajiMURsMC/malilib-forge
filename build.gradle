plugins {
	id "dev.architectury.loom" version "0.12.0-SNAPSHOT"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
	// maven { url 'https://maven.terraformersmc.com/releases/' }
	// maven { url 'https://maven.quiltmc.org/repository/release/' }
}

loom {
    forge {
        mixinConfigs = [
			"mixins.malilib.json"
        ]
    }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.mappings_version}:v2"
	forge "net.minecraftforge:forge:${project.forge_version}"
	implementation "com.google.code.findbugs:jsr305:3.0.2"

	// Fabric API. This is technically optional, but you probably want it anyway.
	//modImplementation "net.fabricmc.fabric-api:fabric-api:" + project.fabric_version

	// modImplementation(fabricApi.module("fabric-resource-loader-v0", project.fabric_api_version))
	// include(modImplementation(fabricApi.module("fabric-resource-loader-v0", project.fabric_api_version)))

	// modCompileOnly "com.terraformersmc:modmenu:${project.mod_menu_version}"
}

group = project.group + "." + project.mod_id
archivesBaseName = project.mod_file_name + '-' + project.minecraft_version_out
version = project.mod_version

if (version.endsWith('-dev')) {
	version += "." + new Date().format('yyyyMMdd.HHmmss')
}

processResources {
	// Exclude the GIMP image files
	exclude '**/*.xcf'
	exclude '**/xcf'

	// this will ensure that this task is redone when the versions change.
	inputs.property "minecraft_version", project.minecraft_version

	inputs.property "mod_version", project.mod_version

	filesMatching("META-INF/mods.toml") {
        expand "version": project.mod_version,
			   "minecraft_version": project.minecraft_version
    }
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
	// add some additional metadata to the jar manifest
    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.mod_name,
                "Implementation-Version"  : mod_version,
                "Implementation-Vendor"   : project.author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
